//name: Defender XDR - Detect Local Administrator Addition
//description:
//This query detects when a user was added to the (local) "Administrators" group.

let Identities = IdentityInfo
| where TimeGenerated > ago(30d) // Needs to be set to maximum time frame of 30d. If not, user information in result output might be empty.
| where isnotempty(OnPremSid) or isnotempty(CloudSid)
| extend AccountSid = iff(
  SourceProvider == "AzureActiveDirectory",
  iff(isnotempty(OnPremSid) and isnotempty(CloudSid), OnPremSid, CloudSid),
  OnPremSid
  )
| distinct AccountSid, AccountDomain, AccountName;
DeviceEvents
| where TimeGenerated > ago(1h) // Change the time frame here as necessary.
| where ActionType == "UserAccountAddedToLocalGroup"
| extend Details = parse_json(AdditionalFields)
| extend GroupSid = tostring(Details.GroupSid)
| where GroupSid == "S-1-5-32-544"
| join kind=leftouter Identities on AccountSid
| project Timestamp, ReportId, DeviceId, InitiatingProcessAccountName, AccountSid, AssignedDomain = AccountDomain1, AssignedUser = AccountName1
