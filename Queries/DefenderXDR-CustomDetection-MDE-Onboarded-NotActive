//name: MDE - Onboarded devices where MDAV is not active
//description:
//Ideally, onboarded devices should have MDAV in active mode. MDAV will go into passive mode when another AV solution is installed or when engine or platform versions are outdated.
//Another use case is when the device is onboarded to MDE if another AV solution came pre-installed with the OS, e.g. HP Wolf Security.
//Use this as a custom detection in Defender XDR to get alerted automatically, e.g. once a day.

let avmodetable = DeviceTvmSecureConfigurationAssessment
| where ConfigurationId == "scid-2010" and isnotempty(Context)
| extend avdata=parsejson(Context)
| extend AVMode = iif(tostring(avdata[0][0]) == '0', 'Active' , iif(tostring(avdata[0][0]) == '1', 'Passive' ,iif(tostring(avdata[0][0]) == '4', 'EDR Blocked' ,'Unknown')))
| where AVMode != "Active"
| project DeviceId, AVMode;
DeviceInfo
| where Timestamp > ago(1d)
| where OnboardingStatus == "Onboarded"
| summarize arg_max(Timestamp,*) by DeviceId
| join avmodetable on DeviceId
| project Timestamp, ReportId, DeviceId, DeviceName, OSPlatform, AVMode
