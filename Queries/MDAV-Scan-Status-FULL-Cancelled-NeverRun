//name: Defender for Antivirus (MDAV) Scan Status - None/Cancelled Full Scans
//updated: 2024-12-9
//description:
//This query will return a report of all devices that were seen within the last day and where a full scan has never been run or was cancelled.
//Use line 9 to filter by device platform, e.g. Windows clients, Windows servers, macOS, ...
//You can then use the "Take action" button in the Advanced Hunting query dashboard to trigger a full scan on one or multiple devices.

DeviceTvmInfoGathering
| where LastSeenTime > ago(1d)
//| where OSPlatform in ("Windows10", "Windows11")
| extend AvScanResults=tostring(AdditionalFields.AvScanResults)
| extend FullScanInfo=extractjson("$.Full", AvScanResults, typeof(string))
| extend FullScanStatus=extractjson("$.ScanStatus", FullScanInfo, typeof(string))
| project-away AdditionalFields, AvScanResults, FullScanInfo
| where FullScanStatus != "Completed"
| sort by DeviceName asc
